apply plugin: 'com.android.application'
apply plugin: 'android-apt'

// 根据日期自动生成build号
def calendar = Calendar.getInstance();
def time = String.format("%d%02d%02d", calendar.get(Calendar.YEAR), calendar.get(Calendar.MONTH) + 1, calendar.get(Calendar.DAY_OF_MONTH));

// 读取local.properties文件
def Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {

    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "org.cnodejs.android.md"
        minSdkVersion 9
        targetSdkVersion 24
        versionCode Integer.parseInt(time)
        versionName "1.2.1-beta.1"

        manifestPlaceholders = [
                UMENG_CHANNEL: "CNodeJS",
                UMENG_APPKEY: properties.getProperty("umeng.appkey")
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'

    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    compile 'com.google.code.gson:gson:2.6.2'
    compile 'net.danlew:android.joda:2.9.3.1'
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.dlazaro66.qrcodereaderview:qrcodereaderview:1.0.0'
    compile 'com.github.chrisbanes:PhotoView:1.2.6'

    compile 'org.tautua.markdownpapers:markdownpapers-core:1.4.4'
    compile 'org.jsoup:jsoup:1.9.2'

    compile 'com.umeng.analytics:analytics:6.0.1'
}
